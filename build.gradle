buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = System.properties['mod_version']
if (version == null) {
	version = "Dev"
}

group = "com.feed_the_beast.ftbutilities"
archivesBaseName = "FTBUtilities"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
	sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
	version = "1.12.1-14.22.0.2474"
	runDir = "run"
	mappings = "snapshot_20170908"
}

repositories {
	ivy { name "com.latmod.mods"; artifactPattern "http://mods.latmod.com/[module]/[revision]/[module]-[revision](-[classifier]).[ext]" }
	maven { url 'https://oss.sonatype.org/content/groups/public/' }
	maven { url 'http://dvs1.progwml6.com/files/maven' }
}

dependencies {
	compile 'com.latmod.mods:FTBLib:4.1.0:api'
	compile 'com.latmod.mods:iChunUtil:1.10.2-6.0.0:api'
	compile 'info.journeymap:journeymap-api:1.11-1.3-SNAPSHOT'
	deobfCompile 'mod.chiselsandbits:chiselsandbits:13.0.78:api'
	deobfCompile 'slimeknights.mantle:Mantle:1.12-1.3.0.9'
	deobfCompile 'slimeknights:TConstruct:1.12-2.8.0.5'
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir 'src/main/api'
		}
		resources {
			srcDir 'src/main/resources'
		}
	}
}

task apiJar(type: Jar) {
	from sourceSets.main.java
	include "com/feed_the_beast/ftbu/api/**"
	from sourceSets.main.output
	include "com/feed_the_beast/ftbu/api/**"
	classifier = 'api'
}

build.dependsOn apiJar

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}