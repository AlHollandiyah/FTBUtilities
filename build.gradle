buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = System.properties['mod_version'];
if (version == null) {
    version = "Dev"
}

group = "com.feed_the_beast.ftbutilities"
archivesBaseName = "FTBUtilities"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.10.2-12.18.3.2185"
    runDir = "run"
    mappings = "snapshot_20161111"
}

repositories {
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    ivy {
        name "LatMod"
        artifactPattern "http://mods.latmod.com/[module]/[revision]/[module]-[revision](-[classifier]).[ext]"
    }
    ivy {
        name "ChiselsAndBits"
        artifactPattern "https://addons-origin.cursecdn.com/files/2341/112/[module]-[revision](-[classifier]).[ext]"
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    compile 'LatMod:FTBLib:1.10.2-3.2.5:api'
    compile 'LatMod:iChunUtil:1.10.2-6.0.0:api'
    compile group: 'info.journeymap', name: 'journeymap-api', version: '1.9.4-1.2', changing: true
    compile 'ChiselsAndBits:chiselsandbits:12.7'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'src/main/api'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

task apiJar(type: Jar) {
    from sourceSets.main.java
    include "com/feed_the_beast/ftbu/api/**"
    from sourceSets.main.output
    include "com/feed_the_beast/ftbu/api/**"
    classifier = 'api'
}

build.dependsOn apiJar

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}