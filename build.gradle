buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${mod_version}"
group = package_group
archivesBaseName = mod_name

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
	sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
	version = "${mc_version}-${forge_version}"
	runDir = "run"
	mappings = "${mappings_version}"

	replace "@VERSION@", project.version
	replaceIn "${mod_reference}"
}

repositories {
	ivy { name "com.latmod.mods"; artifactPattern "http://mods.latmod.com/[module]/[revision]/[module]-[revision](-[classifier]).[ext]" }
	maven { url "https://oss.sonatype.org/content/groups/public/" }
	maven { url "http://dvs1.progwml6.com/files/maven" }
}

dependencies {
	provided "com.latmod.mods:FTBLib:${ftblib_version}:api"
	runtime "com.latmod.mods:FTBLib:${ftblib_version}"
	provided "com.latmod.mods:iChunUtil:${ichunutil_version}:api"
	provided "info.journeymap:journeymap-api:${journeymap_version}"
	deobfProvided "mod.chiselsandbits:chiselsandbits:${chiselsandbits_version}:api"
	deobfProvided "slimeknights.mantle:Mantle:${mantle_version}"
	deobfProvided "slimeknights:TConstruct:${tconstruct_version}"
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir 'src/main/api'
		}
		resources {
			srcDir 'src/main/resources'
		}
	}
}

task apiJar(type: Jar) {
	from sourceSets.main.java
	include "com/feed_the_beast/ftbu/api/**"
	from sourceSets.main.output
	include "com/feed_the_beast/ftbu/api/**"
	classifier = 'api'
}

build.dependsOn apiJar

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}